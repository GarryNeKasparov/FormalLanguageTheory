$INCLUDE "LibraryEx";
$ENUM Table;
$ENUM ExdTable;

Oracle-MEMBER {
    eps = 1;
    '1100' = 1;
    '00' = 1;
    '11' = 1;
    '0110' = 1;
    '011' = 1;
    e.W = 0;
}


$ENTRY ExdTable-Create {
    e.Alphabet
    , <Map {s.a = (s.a); = ;} e.Alphabet> : e.SS
    , <Map {(e.Prefix)e.P-E = <ComputeRow (e.Prefix) (eps)>; = ;} e.SS> : e.V
    = <Br 'Abt='e.Alphabet>
    [ExdTable
    [Table ((eps)) ((eps)) ((eps' 'eps' '1))]
    [Table
        (e.SS)
        ((eps))
        (e.V)]];
}

$ENTRY ExdTable-Print {
    [ExdTable t.T1 t.T2]
    = <Table-Print t.T1><Prout><Table-Print t.T2>;
}

$ENTRY Table-Print {
    [Table (e.S) (e.E) (e.V)]
    , <Prout '\t' <Map {(e.I) = e.I'\t'} e.E>> :
    , <PrintLoop (<Sort e.S>) (e.E) e.V> :
    = ;
}

PrintLoop {
    () (e.E) e.V = ;
    ((e.S) e.S-E) (e.E) e.V
    , <GetRow (e.S) (e.E) e.V ('\t')> : e.Row
    = <Prout e.S'\t' e.Row><PrintLoop (e.S-E) (e.E) e.V>;

}

GetRow {
    (e.S) ((e.E) e.E-E) e.V-B (e.S' 'e.E' 'e.V) e.V-E (e.del)
    = e.V e.del <GetRow (e.S) (e.E-E) e.V-B e.V-E (e.del)>;
    (e.S) (e.E) e.V-B (e.S' 'e.E' 'e.V) e.V-E (e.del)
    = e.V;
    (e.S) (e.Else) e.Some =;
}

GetRowEnum {
    (e.S) ((e.E) e.E-E) e.V-B (e.S' 'e.E' 'e.V) e.V-E (e.del)
    =(e.S (e.V e.del <GetRow (e.S) (e.E-E) e.V-B e.V-E (e.del)>));
    (e.S) (e.E) e.V-B (e.S' 'e.E' 'e.V) e.V-E (e.del)
    = (e.S (e.V));
    (e.S) (e.Else) e.Some =;
}

$ENTRY Table-Update {
    (e.S)
    [Table (e.SS) (e.E) (e.V)] e.CrEx
    , e.SS : {
        e.SS-B (e.CrEx) e.SS-E = e.SS-B e.SS-E;
        e.NoCrEx = e.SS;
    } : e.SS^
    , <AddSymb (Exclude e.S) (e.CrEx) <Cp 'Abt'>> : e.NewP
    , <Map {
        (e.P)e.P-E = <ComputeRow (e.P) e.E>;
        /*empty*/ = ;
      } e.NewP> : e.NewRows
    = [Table (e.SS e.NewP) (e.E) (<DeleteRow (e.CrEx) e.V> e.NewRows)];
}

DeleteRow {
    (e.S) (e.S' 'e.Col) e.V = <DeleteRow (e.S) e.V>;
    (e.S) e.V = e.V;
}

AddSymb {
    (Exclude e.S) (e.Word) /*empty*/ = ;
    (Exclude e.S) (e.Word) s.Char e.Abt
    , e.S : {
        e.S-B (e.Word s.Char) e.S-E = <AddSymb (Exclude e.S) (e.Word) e.Abt>;
        e.SE = (e.Word s.Char) <AddSymb (Exclude e.S) (e.Word) e.Abt>;
    };

}

$ENTRY ExdTable-Insert {
    [ExdTable [Table (e.S-B (e.Prefix) e.S-E) (e.E) (e.V1)] t.T]
    (notclosed) e.Prefix
    = [ExdTable [Table (e.S-B (e.Prefix) e.S-E) (e.E) (e.V1)] t.T];

    [ExdTable [Table (e.S) (e.E) (e.V1)] t.T]
    (notclosed) e.Prefix
    , <ComputeRow (e.Prefix) e.E> : e.NewRow
    , <Table-Update (e.S) t.T e.Prefix> : t.T^
    = [ExdTable [Table (e.S (e.Prefix)) (e.E) (e.V1 e.NewRow)] t.T];

    [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)] ]
    (notconsist) e.Suf
    , e.E (e.Suf) : e.E^
    , <GetNewColumn (e.S) e.Suf (e.V1 e.V2)> : e.V1^
    , <GetNewColumn (e.SS) e.Suf (e.V1 e.V2)> : e.V2^
    = [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)] ];


    [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)] ]
    (counter) e.W
    , <GetAllPrefixes e.W (1)> : e.Prefs
*    , <Prout all prefs e.Prefs> :
*   for all pref in prefs exd-insert pref (notclosed) gute?
    = <InsertAllPrefixes
        [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)]]
        e.Prefs>;
}

GetNewColumn {
    ((e.S)e.S-E) e.Suf (e.V)
    = <FindInTable (e.S e.Suf) e.V> : {
        True e.M = <GetNewColumn (e.S-E) e.Suf (e.V (e.S' 'e.Suf' 'e.M))>;
        False = <GetNewColumn (e.S-E) e.Suf (e.V (e.S' 'e.Suf' '<Oracle-MEMBER e.S e.Suf>))>;
    };
    (/*empty*/) e.Suf (e.V)
    = e.V;
}

FindInTable {
    (e.S s.Suf) e.V-B (e.S s.Suf' 'e.Suf2' 'e.M) e.V-E
    = True e.M;
    (e.S s.Suf) e.V
    = False;
}


InsertAllPrefixes {
    t.ExdTable (e.Pref) e.Prefixes
    , <ExdTable-Insert t.ExdTable (notclosed) e.Pref> : t.ExdTable^
    = <InsertAllPrefixes t.ExdTable e.Prefixes>;
    t.ExdTable /*empty*/
    = t.ExdTable;
}

GetAllPrefixes {
    s.a (1) = (s.a);
    s.a (0) = ; /*уже включен предыдущем вызовом*/
    e.W s.a (1) = (e.W s.a) <GetAllPrefixes e.W s.a (0)>;
    e.W s.a (0) = (e.W) <GetAllPrefixes e.W (0)>;

}

ComputeRow {
    (e.Prefix) /*empty*/ = ;
    (eps) (e.Suffix) e.E
    = (eps' 'e.Suffix<Oracle-MEMBER e.Suffix>)<ComputeRow (eps) e.E>;
    (e.Prefix) (eps) e.E
    = (e.Prefix' 'eps' '<Oracle-MEMBER e.Prefix>)<ComputeRow (e.Prefix) e.E>;
    (e.Prefix) (e.Suffix) e.E
    = (e.Prefix' 'e.Suffix' '<Oracle-MEMBER e.Prefix e.Suffix>)<ComputeRow (e.Prefix) e.E>;
}

$ENTRY ExdTable-IsClosed {
    [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)]]
    , <GetAllRowsEnum (e.S) (e.E) e.V1 ()> : e.Rows1
    , <GetAllRowsEnum (e.SS) (e.E) e.V2 ()> : e.Rows2
*    , <Prout e.Rows1' 'e.Rows2> :
    , <CompareRows (e.Rows1) (e.Rows2)> : {
        True = True [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)]];
        e.Pref = False <ExdTable-Insert [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)]]
        (notclosed) e.Pref>;
    };
}

CompareRows {
    (e.Rows1-B (e.S (e.Row)) e.Rows-E) (e.Rows2-B (e.SS (e.Row)) e.Rows-E)
    = <CompareRows (e.Rows1-B (e.S (e.Row)) e.Rows-E) (e.Rows2-B e.Rows-E)>;
    (e.S (e.Row)) (e.Rows2-B (e.SS (e.Row)) e.Rows-E)
    = <CompareRows (e.S (e.Row)) e.Rows2-B e.Rows-E>;
    (e.Rows)e.Rows-E (/*empty*/) = True;
    (e.Rows)e.Rows-E ((e.S (e.Row))e.Else) = e.S;
}

GetAllRows {
    ((e.S) e.S-E) (e.E) e.V
    = (<GetRow (e.S) (e.E) e.V ()>)<GetAllRows (e.S-E) (e.E) e.V>;
    (/*empty*/) (e.E) e.V
    = ;
    (e.S) (e.E) e.V
    = (<GetRow (e.S) (e.E) e.V ()>);
}
GetAllRowsEnum {
    ((e.S) e.S-E) (e.E) e.V
    = (e.S (<GetRow (e.S) (e.E) e.V ()>))<GetAllRowsEnum (e.S-E) (e.E) e.V>;
    (/*empty*/) (e.E) e.V
    = ;
    (e.S) (e.E) e.V
    = (e.S (<GetRow (e.S) (e.E) e.V ()>));
}

CompareItems {
    (e.S1' 'e.E1' 's.V) e.S2' 'e.E2' 's.V = True;
    (e.S1' 'e.E1' 's.V1) e.S2' 'e.E2' 's.V2 = False;
}

$ENTRY ExdTable-ToDFA {
    [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)]]
    , <GetAllRowsEnum (e.S) (e.E) e.V1> : e.T1Rows
    , <GetAllRowsEnum (e.SS) (e.E) e.V2> : e.T2Rows
    , <Prout all rows' 'e.T1Rows> :
    , <SmallestUniq e.T1Rows> : e.States
    , <Prout uniq rows' 'e.States> :
    , <GetSDict e.States (0)> : e.StatesEnum
    , <DivideStates e.States () ()> : (e.Init)e.F
    , <GetTransitions (e.States) e.T1Rows e.T2Rows> : e.Trans
    , <Prout Finals' 'e.F'\n'All' 'e.StatesEnum'\n'Transitions' 'e.Trans> :

    , <DFANF (e.StatesEnum) (e.Init) (e.F) e.Trans> : e.DFA
    = e.DFA;
}

DecodeState {
    e.S (e.D-B (e.S (e.Row)(e.N)) e.D-E)
    = e.N;
}

DecodeTrans {
    (e.q0)s.t(e.q1) (e.D)
    , e.D : e.D-B (e.S (e.q0)(e.N0)) e.D-E
    , e.D : e.D-B^ (e.S^ (e.q1)(e.N1)) e.D-E^
    = <Symb e.N0>':'s.t'>'e.N1;
}

DFANF {
    (e.States (e.S (e.Row)(e.Max))) (e.Init) (e.F) e.Trans
    = '#states\n'e.Max
      '\ninitial\n'<Map {
        (e.State) = <DecodeState e.State (e.States (e.S (e.Row)(e.Max)))>'\n'
      } e.Init>
      'accepting\n'<Map {
        (e.State) = <DecodeState e.State (e.States (e.S (e.Row)(e.Max)))>'\n'
      } e.F>
      'alphabet\n'<Map {s.c = s.c'\n'} <Cp 'Abt'>>
      'transitions\n'<Map {
        (e.T) = <DecodeTrans e.T (e.States (e.S (e.Row)(e.Max)))>'\n'
      } e.Trans>
}


GetTransitions {
    (/*empty*/) e.Rows = ;
    ((e.S) e.S-E) e.Rows
    = <FindNgbrs (e.S) <Cp 'Abt'> (e.Rows)>
      <GetTransitions (e.S-E) e.Rows>;
}

FindNgbrs {
    (e.S (e.Row)) e.Abt s.C (e.Rows)
    , <ExdView2 e.S s.C> : e.SS
    , e.Rows : {
        e.Rows-B (e.SS (e.Row2)) e.Rows-E = ((e.Row) s.C (e.Row2)) <FindNgbrs (e.S (e.Row)) e.Abt (e.Rows-B e.Rows-E)>; /* ДКА => других переходов нет */
        e.RowsE = <Prout (e.SS (e.Row)) not in e.RowsE> <FindNgbrs (e.S (e.Row)) e.Abt (e.RowsE)>;
    };
    (e.S) /*empty*/ (e.Rows) = ;
}
ExdView2 {
    eps s.C = s.C;
    e.W s.C = e.W s.C;
}

SmallestUniq {
    e.R1-B (e.S1 (e.Row1)) e.R1-E e.R2-B (e.S2 (e.Row1)) e.R2-E
    , <Prout found' '(e.S2 (e.Row1))' 'like' '(e.S1 (e.Row1))> :
    , <Shorter (e.S1) e.S2> : {
        2 = <SmallestUniq e.R1-B e.R1-E e.R2-B (e.S2 (e.Row1)) e.R2-E>;
        e.NoMatter = <SmallestUniq e.R1-B (e.S1 (e.Row1)) e.R1-E e.R2-B e.R2-E>;
    };
    e.Rows = e.Rows;
}

Shorter {
    (e.S1 s.a1) e.S2 s.a2
    = <Shorter (e.S1) e.S2>;
    (/*empty*/) /*empty*/ = 0;
    (/*empty*/) e.S2 = 1;
    (e.S1) /*empty*/ = 2;
}

GetSDict {
    (e.S (e.R))e.S-E (e.N)
     = (e.S (e.R)(e.N)) <GetSDict e.S-E (<Inc e.N>)>;
    /*empty*/ (e.N) = ;
}

DivideStates {
    /* здесь 1 стоит заменить на '1' наверно*/
    (e.S (1 e.Rest)) e.S-E (e.Init) (e.F)= <DivideStates e.S-E (e.Init) (e.F (e.S))>;
    (e.S (0 e.Rest)) e.S-E (e.Init) (e.F) = <DivideStates e.S-E (e.Init (e.S)) (e.F)>;
    /*empty*/ (e.Init) (e.F) = (e.Init) e.F;
}

$ENTRY ExdTable-IsConsist {
    [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)]]
    , <GetAllRowsEnum (e.S) (e.E) e.V1> <GetAllRowsEnum (e.SS) (e.E) e.V2>: e.Rows
*    , <Prout All e.Rows> :
    , <OutLoopC e.Rows (e.E) (e.V1 e.V2)> : {
        True = True [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)]];
        (e.S1 (e.Row1))(e.S2 (e.Row2))
        , <Common (e.S1) e.S2> : e.Common
*        , <Prout (e.S1) (e.S2)> :
        = e.Common;
    } : e.Suf
    = False
        <ExdTable-Insert
        [ExdTable [Table (e.S) (e.E) (e.V1)] [Table (e.SS) (e.E) (e.V2)]]
        (notconsist) e.Suf>;
}

Common {
    (e.P1 s.a e.Common) e.P2 s.a e.Common = s.a e.Common;
}

OutLoopC {
    (e.Row1) (e.Row2) e.Rows (e.E) (e.V2)
    , <InLoopC (e.Row1) (e.Row2) e.Rows (e.E) (e.V2)> : {
        True = <OutLoopC (e.Row2) e.Rows (e.E) (e.V2)>;
        e.Else = e.Else;
    };
    (e.Row) /*empty*/ (e.E) (e.V2) = True;
}

InLoopC {
    (e.Row1) /*empty*/ (e.E) (e.V2)
    = True;
    (e.S1 (e.Row1)) (e.S2 (e.Row1)) e.Rows (e.E) (e.V2)
*    , <Prout e.S1 same e.S2> :
    = <AbtLoopC (e.S1 (e.Row1)) (e.S2 (e.Row1)) (e.Rows) <Cp 'Abt'> (e.E) (e.V2)>;
    (e.S1 (e.Row1)) (e.S2 (e.Row2)) e.Rows (e.E) (e.V2)
    = <InLoopC (e.S1 (e.Row1)) e.Rows (e.E) (e.V2)>;
}

AbtLoopC {
    (e.Row1) (e.Row2) (e.Rows) s.s e.Abt (e.E) (e.V2)
    , <ExdView e.Row1 s.s> : e.S12
    , <ExdView e.Row2 s.s> : e.S22
    , <GetRowEnum (e.S12) (e.E) e.V2 ()> : {
        /*empty*/ = True;
        (e.SS1 (e.Row1E))
        , <GetRowEnum (e.S22) (e.E) e.V2 ()> : {
            (e.SS2 (e.Row1E)) = <AbtLoopC (e.Row1) (e.Row2) (e.Rows) e.Abt (e.E) (e.V2)>;
            (e.SS2 (e.Else)) = (e.SS1 (e.Row1E)) (e.SS2 (e.Else));
            /*empty*/ = True;
        };
    };

    (e.Row1) (e.Row2) (e.Rows) (e.E) (e.V2)
    = <InLoopC (e.Row1) e.Rows (e.E) (e.V2)>;
}

ExdView {
    eps (e.Row) s.s = s.s;
    e.S (e.Row) s.s = e.S s.s;
}

GetS {
    e.S' 'e.E' 's.V = e.S;
}

GetE {
    e.S' 'e.E' 's.V = e.E;
}

$ENTRY Go {
*    , <ExdTable-Create '01'> : t.ExdTable
**    , <ExdTable-Print t.ExdTable> :
**    , <Prout>:
*    , <ExdTable-IsClosed t.ExdTable> : e.Status t.ExdTable^
**    , <Prout e.Status> :
**    , <ExdTable-Print t.ExdTable> :
*    , <Prout>:
*    , <ExdTable-Insert t.ExdTable (counter) '11'> : t.ExdTable^
**    , <Prout t.ExdTable> :
**    , <ExdTable-Print t.ExdTable> :
**    , <ExdTable-Print t.ExdTable> :
*    , <ExdTable-IsConsist t.ExdTable> : e.Status^ t.ExdTable^
**    , <ExdTable-Print t.ExdTable> :
*    , <ExdTable-Insert t.ExdTable (counter) '011'> : t.ExdTable^
*    , <ExdTable-Print t.ExdTable> :
*    , <ExdTable-ToDFA t.ExdTable> :
    , [ExdTable
          [Table ((eps)('b')('ba')('bb')) ((eps)('a'))
           ((eps' 'eps' '0)(eps' ''a'' '0)('b 'eps' '0)('b a '1)
           ('ba 'eps' '1)('ba a '0)('bb 'eps' '1)('bb a '1))]
          [Table (('a')('baa')('bab')('bba')('bbb'))
              ((eps)('a'))
              (('a 'eps' '0)('a a '0)('baa 'eps' '0)('baa a '0)('bab 'eps' '0)('bab a '1)
              ('bba 'eps' '1)('bba a '0)('bbb 'eps' '1)('bbb a '1))
          ]] : t.ExdTable
    , <Br 'Abt=ab'>:
    , <ExdTable-Print t.ExdTable> :

    , <ExdTable-ToDFA t.ExdTable> : e.DFA
    = <Prout e.DFA>;

}